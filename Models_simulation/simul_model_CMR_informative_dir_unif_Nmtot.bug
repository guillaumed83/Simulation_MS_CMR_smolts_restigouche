model{



## Hyper priors for hierachical structure
## of trap efficiencies

for (i in 1:5){
	mu_theta[i] ~ dnorm(0,0.01)
	sigma_theta[i] ~ dunif(0,10)
	tau_theta[i] <- pow(sigma_theta[i],-2)
}

alpha_lambda <- mu_lambda * beta_lambda
mu_lambda ~ dgamma(1,0.0001)	
beta_lambda ~ dgamma(0.01,0.01) 

## The first 3 wheels have their own estimate
for (t in 1:Y){
	for (i in 1:3){
	# When I_w == 0, the loop is skipped	
		for (k in 1:I_w[t,i]){
			Nm[t,i] <- round(p_smolt_prod[t,i] * Nm_tot[t]) 
			C[t,i] ~ dbin(theta[t,i],Nm[t,i])
			L_theta[t,i] ~dnorm(mu_theta[i],tau_theta[i])
			logit(theta[t,i]) <-  L_theta[t,i]
			R[t,i] ~ dbin(theta[t,i],M[t,i])
		}
	}
}

## Total number of smolts
## This is to represent the fact that the estimation of the non marked smolts of the upstream wheels 
## (Kedgwick, Upsalquitch and Matapedia) is linked to the total non marked smolts estimated at the downstream 
## wheels (Moses and Butters)

for( t in 1:Y){
	
## In this loop we calculate the relative production of smolts of each main branch 
## we center that contribution around the proportion wetted area of each trib 
##
## Note that  because not all wheels are running every year , the relative contribution of Nm_rest changes every year
## 
		for (k in 1:n_p_dir[t]){
			p_smolt_prod[t,I_p[t,k]] <- delta[t,k] / sum( delta[t,1:n_p_dir[t]] )
			delta[t,k] ~ dgamma(alpha[t,I_p[t,k]],1)
			}
		
		Nm_rest[t] <- (1-  I_w[t,1]*p_smolt_prod[t,1] -  I_w[t,2]*p_smolt_prod[t,2] - I_w[t,3]*p_smolt_prod[t,3] ) * Nm_tot[t] 
		Nm_tot[t] ~ dunif(0,5000000) 
		
	 
}

## Downstream wheels 
for (t in 1:Y){
	for (i in 4:5){
		# When I_w == 0, the loop is skipped	
		for (k in 1:I_w[t,i]){
			C[t,i] ~ dbin(theta[t,i],round(Nm_tot[t]))
			L_theta[t,i] ~ dnorm(mu_theta[i],tau_theta[i])
			logit(theta[t,i]) <-  L_theta[t,i]
			R[t,i] ~ dbin(theta[t,i],M[t,i])	
		}
	}
}


} ## End Model
